apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    jcenter()
    maven {// SpigotMC
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {// For PlaceholderAPI
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

targetCompatibility = 1.8
sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jar {
    from {
        configurations.compile.collect {
            if (it.name.matches("(guava)-.+\\.jar\$")) return null
            it.isDirectory() ? it : zipTree(it)
        }.stream().filter({ it != null }).collect()
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12.1-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.12-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.0.8'
    compileOnly 'org.yaml:snakeyaml:1.18'

    // ORM
    compile 'com.j256.ormlite:ormlite-core:5.0'
    compile 'com.j256.ormlite:ormlite-jdbc:5.0'
    // CGLIB
    compile 'cglib:cglib:3.2.5'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:22.0'
    testCompile 'org.spigotmc:spigot-api:1.12.1-R0.1-SNAPSHOT'
    testCompile 'net.md-5:bungeecord-api:1.12-SNAPSHOT'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.yunshanmc'
            artifactId 'LMC-Core'
            version '1.0.0-SNAPSHOT'

            from components.java
        }
    }
}